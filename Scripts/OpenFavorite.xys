/* Opens a list containing "Favorites Files" (XYplorer definition â‡’ can contain folders and files) to quickly go to the selected favorite.
 */
"OpenFavorite"
    // Constants
    $blue = "106FCD";

    // Get Favorite Files
    $favoriteFiles = "";
    foreach ($favoriteFile, Favs('f',, '|'), '|', 'e') {
        if ($favoriteFile != '-') { // Ignore separators
            // Remove caption (format is: "Caption" Path)
            $favoriteFiles = $favoriteFiles . RegexReplace($favoriteFile, "("".+"") *(.+)", "$2") . '|';
        }
    }
    if ($favoriteFiles != "") {
        $favoriteFiles = SubStr($favoriteFiles, 0, -1);
        $favoriteFiles = FormatList($favoriteFiles, 's', '|');
    } else {
        Msg("There are no Favorite Files.", 64);
        End(true);
    }

    // Ask the user to select an item
    $selectedItem = InputSelect(
        "Open favorite folder / file" . <crlf> . 
        "Press Shift to open a folder in a new tab." . <crlf> . 
        "Press Ctrl when selecting a file to open its parent folder (can be combined with Shift)." . <crlf> . 
        "Press Alt to remove the selected item from the favorites.", 
        $favoriteFiles, '|', 1+32+64+4096,, 800, 500, "Open favorite");

    // Get pressed modifiers
    $modifierShift = false;
    $modifierCtrl = false;
    $modifierAlt = false;
    if (Get("Shift") == 1) {
        $modifierShift = true;
    } elseif (Get("Shift") == 2) {
        $modifierCtrl = true;
    } elseif (Get("Shift") == 3) {
        $modifierShift = true;
        $modifierCtrl = true;
    } elseif (Get("Shift") == 4) {
        $modifierAlt = true;
    }

    // Get item type (file / folder)
    if (Exists($selectedItem) == 1) {
        // This is a file
        $selectedItemExist = true;
        $selectedItemIsAFile = true;
    } elseif (Exists($selectedItem) == 2) {
        // This is a folder or drive
        $selectedItemExist = true;
        $selectedItemIsAFile = false;
    } else {
        // Item doesn't exist
        $selectedItemExist = false;
    }

    if (!$selectedItemExist || $modifierAlt) {
        if (!$selectedItemExist) {
            Msg("The selected item does not exist. Would you like to remove it from the Favorites Files?" . <crlf> . <crlf> . $selectedItem, 32 + 1); // Script will end if user clicks on Cancel
        } elseif ($modifierAlt) {
            Msg("Remove the following item from the Favorite Files?" . <crlf> . <crlf> . $selectedItem, 32 + 1); // Script will end if user clicks on Cancel
        }
        // Remove item from Favorite Files
        Favs('f', $selectedItem, , 'd');
        Status("Removed from Favorite Files: " . $selectedItem, $blue, "ready");
        End(true);
    } 

    if ($selectedItemIsAFile) {
        if ($modifierCtrl && $modifierShift) {
            // Open parent folder in new tab
            Tab("new", GetPathComponent($selectedItem, "path"));
        } elseif ($modifierCtrl && !$modifierShift) {
            // Open parent folder in current tab
            Goto(GetPathComponent($selectedItem, "path"));
        } else {
            // Open file
            open $selectedItem;
        }
    } else {
        if ($modifierShift) {
            // Open in new tab
            Tab("new", $selectedItem);
        } else {
            // Open in current tab
            Goto($selectedItem);
        }
    }
