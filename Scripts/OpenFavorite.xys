/* Opens a list containing favorites folders and files to quickly go to the selected favorite.
 */
"OpenFavorite"
    // Get favorite folders
    $favoriteFolders = "";
    foreach ($favoriteFolder, Favs('d',, '|'), '|', 'e') {
        if ($favoriteFolder != '-') { // Ignore separators
            // Remove caption (format is: "Caption" Path)
            $favoriteFolders = $favoriteFolders . RegexReplace($favoriteFolder, "("".+"") *(.+)", "$2") . '|';
        }
    }
    if ($favoriteFolders != "") {
        $favoriteFolders = SubStr($favoriteFolders, 0, -1);
        $favoriteFolders = FormatList($favoriteFolders, 's', '|');
    }

    // Get favorite files
    $favoriteFiles = "";
    foreach ($favoriteFile, Favs('f',, '|'), '|', 'e') {
        if ($favoriteFile != '-') { // Ignore separators
            // Remove caption (format is: "Caption" Path)
            $favoriteFiles = $favoriteFiles . RegexReplace($favoriteFile, "("".+"") *(.+)", "$2") . '|';
        }
    }
    if ($favoriteFiles != "") {
        $favoriteFiles = SubStr($favoriteFiles, 0, -1);
        $favoriteFiles = FormatList($favoriteFiles, 's', '|');
    }

    // Concat favorites folders and files
    if ($favoriteFolders != "" && $favoriteFiles != "") {
        $favorites = $favoriteFolders . '|' . $favoriteFiles;
    } elseif ($favoriteFolders != "" && $favoriteFiles == "") {
        $favorites = $favoriteFolders;
    } elseif ($favoriteFolders == "" && $favoriteFiles != "") {
        $favorites = $favoriteFiles;
    } else {
        Msg("There are no favorite folders / files.", 64);
        End(true);
    }

    // Ask the user to select an item
    $selectedItem = InputSelect(
        "Open favorite folder / file" . <crlf> . 
        "Press Shift to open a folder in a new tab." . <crlf> . 
        "Press Ctrl when selecting a file to open its parent folder (can be combined with Shift)." . <crlf> . 
        "Press Alt to remove the selected item from the favorites.", 
        $favorites, '|', 1+32+64+4096,, 800, 500, "Open favorite");

    // Get pressed modifiers
    $modifierShift = false;
    $modifierCtrl = false;
    $modifierAlt = false;
    if (Get("Shift") == 1) {
        $modifierShift = true;
    } elseif (Get("Shift") == 2) {
        $modifierCtrl = true;
    } elseif (Get("Shift") == 3) {
        $modifierShift = true;
        $modifierCtrl = true;
    } elseif (Get("Shift") == 4) {
        $modifierAlt = true;
    }

    // Get item type (file / folder)
    if (Exists($selectedItem) == 1) {
        // This is a file
        $selectedItemExist = true;
        $selectedItemIsAFile = true;
    } elseif (Exists($selectedItem) == 2) {
        // This is a folder or drive
        $selectedItemExist = true;
        $selectedItemIsAFile = false;
    } else {
        // Item doesn't exist
        $selectedItemExist = false;
    }

    if (!$selectedItemExist) {
        Msg("The selected item does not exist. Would you like to remove it from the favorites?" . <crlf> . <crlf> . $selectedItem, 32 + 1); // Script will end if user clicks on Cancel
        // We have no way of knowing if the item was a file or a folder, so try to remove it from both lists
        Favs('f', $selectedItem, , 'd');
        Favs('d', $selectedItem, , 'd');
        Status("Removed from favorites: " . $selectedItem, "0078D7" , "ready");
        End(true);
    }

    if ($modifierAlt) {
        if ($selectedItemIsAFile) {
            // Remove file from favorites
            Msg("Remove the following file from the favorites?" . <crlf> . <crlf> . $selectedItem, 32 + 1); // Script will end if user clicks on Cancel
            Favs('f', $selectedItem, , 'd');
            Status("Removed from favorite files: " . $selectedItem, "0078D7" , "ready");
            End(true);
        } else {
            // Remove folder from favorites
            Msg("Remove the following folder from the favorites?" . <crlf> . <crlf> . $selectedItem, 32 + 1); // Script will end if user clicks on Cancel
            Favs('d', $selectedItem, , 'd');
            Status("Removed from favorite folders: " . $selectedItem, "0078D7", "ready");
            End(true);
        }
    }

    if ($selectedItemIsAFile) {
        if ($modifierCtrl && $modifierShift) {
            // Open parent folder in new tab
            Tab("new", GetPathComponent($selectedItem, "path"));
        } elseif ($modifierCtrl && !$modifierShift) {
            // Open parent folder in current tab
            Goto(GetPathComponent($selectedItem, "path"));
        } else {
            // Open file
            open $selectedItem;
        }
    } else {
        if ($modifierShift) {
            // Open in new tab
            Tab("new", $selectedItem);
        } else {
            // Open in current tab
            Goto($selectedItem);
        }
    }
